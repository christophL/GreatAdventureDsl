grammar at.ac.uibk.greatAdventure.GreatAdventure with org.eclipse.xtext.common.Terminals

generate greatAdventure "http://www.ac.at/uibk/greatAdventure/GreatAdventure"

Adventure:
	title = AdventureTitle 
	((itemDef += ItemDefinition) | (sceneDef += SceneDefinition))*
	startDef = StartDefinition
	((itemDef += ItemDefinition) | (sceneDef += SceneDefinition))*
;
	
AdventureTitle:
	name=STRING
;
ItemDefinition:
	("A" | "An" | "Ms." | "Mrs." | "Mr.") name=STRING ":"
	( 
		( pickup ?= "- can be picked up")?
		&
		"- looks like" img=STRING
		&
		(uses+=UseDefinition)*
		&
		(dialog+=DialogDefintion)?
	)
;

UseDefinition:
	"- you can" name+=STRING "with"? ("it, which:" | "her, which:" | "him, which") 
	(
		("+ requires" reqs+=[ItemDefinition] ("or says" altText+=STRING)?)?
		&
		("+ adds" adds+=[ItemDefinition] ("," adds+=[ItemDefinition])*)?
		&
		("+ removes" dels+=[ItemDefinition] ("," dels+=[ItemDefinition])*)?
		&
		("+ says" text+=STRING)?
		&
		("+ takes you to the" followScene=[SceneDefinition])?
	)
	
;

DialogDefintion:
	"- talks with you:"
		talk = TalkDefinition
;

TalkDefinition:
	(("+ it says" | "+ she says" | "+ he says") say=STRING)
	&
	(("+ it adds" | "+ she adds" | "+ he adds") adds+=[ItemDefinition] ("," adds+=[ItemDefinition])*)?
	&
	(("+ it removes" | "+ she removes" | "+ he removes") dels+=[ItemDefinition] ("," dels+=[ItemDefinition])*)?
	&
	(((answers += AnswerDefinition)+ "+ no more answers") | "+ you do not answer")	
;

AnswerDefinition:
	"+ you say" answer=STRING ":"
		talk=TalkDefinitionMinus
;

TalkDefinitionMinus:
	(("- it says" | "- she says" | "- he says") say=STRING)
	&
	(("- it adds" | "- she adds" | "- he adds") adds+=[ItemDefinition] ("," adds+=[ItemDefinition])*)?
	&
	(("- it removes" | "- she removes" | "- he removes") dels+=[ItemDefinition] ("," dels+=[ItemDefinition])*)?
	&
	((answers += AnswerDefinitionMinus)+ "- no more answers" | "- you do not answer")
;

AnswerDefinitionMinus:
	"- you say" answer=STRING ":"
		talk=TalkDefinition
;

SceneDefinition:
	"The" name=STRING "looks like" img=STRING 
	(("and in it are:" items += ItemPositionDefinition+) | ".")
	
;

ItemPositionDefinition:
	"- a" item=[ItemDefinition] ("at" "(" xpos=INT "," ypos=INT ")")?
;

StartDefinition:
	"In the beginning you:" 
	(
		"- are at the" startLoc=[SceneDefinition]
		&
		("- have" startItems+=[ItemDefinition] ("," startItems+=[ItemDefinition])*)?
	) 
;



